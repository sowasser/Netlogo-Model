turtles-own [
  shoalmates         ;; agentset of nearby turtles
  nearest-neighbor   ;; closest one of our shoalmates
]

globals [ scale ]

to setup
  clear-all
  set scale 0.25 ;; scale lets us have a bigger world without too many patches
  create-turtles population
    [ set color cyan - 2 + random 7  ;; random shades look nice
      setxyz random-xcor
             random-ycor
             random-zcor
      right random-float 360
      tilt-up asin (1.0 - random-float 2.0)
      roll-right random-float 360
      set shoalmates no-turtles ]
  ask turtle 0 [ set color red ]
  reset-ticks
end

to go
  ask turtles
    [ shoal ]
  repeat 5
  [
    ask turtles
    [
      fd scale / 2
    ]
    display
  ]
  tick
end

to shoal  ;; turtle procedure
  find-shoalmates
  if any? shoalmates
    [ find-nearest-neighbor
      ifelse distance nearest-neighbor < ( minimum-separation * scale )
        [ separate ]
        [ align
          cohere ] ]
end

to find-shoalmates  ;; turtle procedure
  set shoalmates other turtles in-radius (vision * scale)
end

to find-nearest-neighbor ;; turtle procedure
  set nearest-neighbor min-one-of shoalmates [distance myself]
end

;;; SEPARATE

to separate  ;; turtle procedure
  turn-away ([heading] of nearest-neighbor) max-separate-turn
  pitch-away ([pitch] of nearest-neighbor) max-separate-turn
end

;;; ALIGN

to align  ;; turtle procedure
  turn-towards average-shoalmate-heading max-align-turn
  pitch-towards average-shoalmate-pitch max-align-turn
end

to-report average-shoalmate-heading  ;; turtle procedure
  ;; We can't just average the heading variables here.
  ;; For example, the average of 1 and 359 should be 0,
  ;; not 180.  So we have to use trigonometry.
  report atan sum [sin heading] of shoalmates
              sum [cos heading] of shoalmates
end

to-report average-shoalmate-pitch  ;; turtle procedure
  ;; We can't just average the heading variables here.
  ;; For example, the average of 1 and 359 should be 0,
  ;; not 180.  So we have to use trigonometry.
  report atan sum [sin pitch] of shoalmates
              sum [cos pitch] of shoalmates
end

;;; COHERE

to cohere  ;; turtle procedure
  turn-towards average-heading-towards-shoalmates max-cohere-turn
  pitch-towards average-pitch-towards-shoalmates max-cohere-turn
end

to-report average-heading-towards-shoalmates  ;; turtle procedure
  ;; "towards myself" gives us the heading from the other turtle
  ;; to me, but we want the heading from me to the other turtle,
  ;; so we add 180
  report atan mean [sin (towards myself + 180)] of shoalmates
              mean [cos (towards myself + 180)] of shoalmates
end

to-report average-pitch-towards-shoalmates  ;; turtle procedure
  report mean [0 - (towards-pitch myself)] of shoalmates
end

;;; HELPER PROCEDURES

to turn-towards [new-heading max-turn]  ;; turtle procedure
  turn-at-most (subtract-headings new-heading heading) ( max-turn * scale )
end

to turn-away [new-heading max-turn]  ;; turtle procedure
  turn-at-most (subtract-headings heading new-heading) ( max-turn * scale )
end

to pitch-towards [new-pitch max-turn]  ;; turtle procedure
  pitch-at-most (subtract-headings new-pitch pitch) ( max-turn * scale )
end

to pitch-away [new-pitch max-turn]  ;; turtle procedure
  pitch-at-most (subtract-headings pitch new-pitch) ( max-turn * scale )
end

;; turn right by "turn" degrees (or left if "turn" is negative),
;; but never turn more than "max-turn" degrees
to turn-at-most [turn max-turn]  ;; turtle procedure
  ifelse abs turn > max-turn
    [ ifelse turn > 0
        [ rt max-turn ]
        [ lt max-turn ] ]
    [ rt turn ]
end

to pitch-at-most [turn max-turn]  ;; turtle procedure
  ifelse abs turn > max-turn
    [ ifelse turn > 0
        [ tilt-up max-turn ]
        [ tilt-down max-turn ] ]
    [ tilt-up turn ]
end


; Copyright 1998 Uri Wilensky.
; See Info tab for full copyright and license.